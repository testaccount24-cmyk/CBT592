*---------------------------------------------------------------------*
*                                                                     *
*           This program provides a method to quickly "DISPLAY"       *
*           data.  This function is similar to the COBOL 'EXHIBIT     *
*           NAMED'.  This data can be used to trace program execution,*
*           display data, verify the results of program execution,    *
*           or any other similar use.  This program allows data to    *
*           be printed (DISPLAYed) without having to add an extra     *
*           DCB, OPEN it, CLOSE it, or format the data before         *
*           printing.  Output is double spaced between invocations    *
*           and a sequence number is printed to group multiple lines  *
*           Of one DISPLAY together if all data cannot be DISPLAYed   *
*           on the same line.                                         *
*                                                                     *
*           This module is NOT re-entrant.                            *
*                                                                     *
*           The parameter list can be generated via the 'DISPLAY'     *
*           macro.                                                    *
*                                                                     *
*           Some data types have maximum lengths that must not be     *
*           exceeded.  These maximums are:                            *
*               Binary            12                                  *
*               Character        115                                  *
*               Hex               57                                  *
*               Packed decimal    15                                  *
*          The 'DISPLAY' macro will check lengths and issue MNOTES    *
*          for any values which exceed these maximums.  If registers  *
*          are used to pass a variable length, the module will        *
*          truncate any lengths which exceed the maximum.             *
*                                                                     *
*          No edit checking is done on packed decimals to ensure      *
*          valid values before attempting to EDit.                    *
*                                                                     *
*---------------------------------------------------------------------*
DISPLAY  CSECT
         USING DISPLAY,R12              DECLARE ENTRY BASE
         USING DSPLDSCT,R11             DECLARE PARM BLOCK BASE
         PRINT ON,NOGEN                 NO MACRO PRINT
         STM   R14,R12,12(R13)          STORE CALLERS REGISTERS
         LR    R12,R15                  LOAD BASE REGISTER
         LR    R15,R13                  SAVE CALLERS SAVEAREA ADDR
         LA    R13,SAVE                 LOAD ADDRESS OF MY SAVE AREA
         ST    R15,4(,R13)              CHAIN CALLERS TO MINE
         ST    R13,8(,R15)              CHAIN MINE TO CALLERS
         LR    R11,R1                   COPY PARAMETER LIST ADDRESS
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
INIT0000 DS    0H
         TM    DCBFLAG,$DCBOPEN         IS THE DCB OPEN??
         BO    PROC0000                 YES
         OPEN  (DISPDCB,OUTPUT)         OPEN 'DISPLAY' DCB
         OI    DCBFLAG,$DCBOPEN         DCB IS OPEN
         BAL   R10,HEADINGS             PRINT INITIAL HEADINGS
PROC0000 DS    0H
         MVI   PRCC,C'0'                CAUSE A DOUBLE SPACE
         AP    SEQNBR,P1                ADD 1 TO SEQUENCE NUMBER
         MVC   SEQWORK,SEQEDWD          SET EDIT WORD
         ED    SEQWORK,SEQNBR           EDIT SEQUENCE NUMBER
         MVC   PRSEQ,SEQWORK+1          MOVE EDITED SEQUENCE NUMBER
PROC0010 DS    0H
         LA    R3,PRDATA                INITIALIZE DATA ADDRESS
         LA    R4,124                   INITIALIZE LENGTH REMAINING
         B     PROC0030                 SKIP 'NEXT' ELEMENT STUFF
PROC0020 DS    0H
         LA    R11,DSPLLEN(R11)         NEXT DATA ELEMENT
         C     R12,DSPLNAME             END OF LIST??
         BE    EXIT0000                 YES, EXIT
         LA    R3,2(R3)                 SKIP 2 SPACES
         SH    R4,H2                    ACCOUNT FOR THEM
         BNH   PRT0000                  LINE FULL
PROC0030 DS    0H
         L     R5,DSPLADDR              DATA ITEM ADDRESS
         CLI   DSPLTYPE,C'C'            CHARACTER DATA??
         BE    PROC1000                 YES
         CLI   DSPLTYPE,C'X'            HEX DATA??
         BE    PROC2000                 YES
         CLI   DSPLTYPE,C'P'            PACKED DECIMAL??
         BE    PROC3000                 YES
         CLI   DSPLTYPE,C'F'            FULLWORD??
         BE    PROC4000                 YES
         CLI   DSPLTYPE,C'H'            HALFWORD??
         BE    PROC5000                 YES
         CLI   DSPLTYPE,C'B'            BINARY??
         BE    PROC6000                 YES
         CLI   DSPLTYPE,C'R'            REGISTER CONTENTS?
         BE    PROC7000                 YES
         DC    H'0'                     .. BAD NEWS
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*         PROCESS CHARACTER FIELDS                                    *
*                                                                     *
*---------------------------------------------------------------------*
PROC1000 DS    0H
         SH    R4,H9                    LENGTH OF NAME AND ':'
         BNH   PRT0000                  NAME WON'T FIT ON THIS LINE
         LH    R1,DSPLDLEN              DATA LENGTH
         CH    R1,H115                  EXCEED MAXIMUM??
         BNH   PROC1010                 NO
         LH    R1,H115                  LIMIT LENGTH
PROC1010 DS    0H
         CR    R4,R1                    WILL DATA FIT ALSO??
         BL    PRT0000                  NO ... PRINT WHAT WE HAVE
         MVC   0(8,R3),DSPLNAME         MOVE DATA ELEMENT NAME
         MVI   8(R3),C':'               INSERT COLON
         LA    R3,9(R3)                 NEW OUTPUT ADDRESS
         LTR   R1,R1                    LENGTH ZERO??
         BZ    PROC0020                 NO DATA
         SR    R4,R1                    DECREMENT LENGTH LEFT ON LINE
         BCTR  R1,0                     ADJUST FOR EXECUTE
         EX    R1,PROC1MVC              MOVE DATA TO PRINT I/O AREA
         EX    R1,PROC1TR               TRANSLATE OUT UNPRINTABLES
         LA    R3,1(R1,R3)              NEW PRINT LINE ADDRESS
         B     PROC0020                 ADVANCE TO NEXT DATA ELEMENT
PROC1MVC MVC   0(0,R3),0(R5)            MOVE DATA TO PRINT I/O AREA
PROC1TR  TR    0(0,R3),PROC1TBL         TRANSLATE OUT UNPRINTABLES
PROC1TBL DS    0CL256
*                0123456789ABCDEF
         DC    C'                '      X'00' - X'0F'
         DC    C'                '      X'10' - X'1F'
         DC    C'                '      X'20' - X'2F'
         DC    C'                '      X'30' - X'3F'
         DC    C'           .<(+|'      X'40' - X'4F'
         DC    C'&&         !$*);¬'     X'50' - X'5F'
         DC    C'-/        ¦,%_>?'      X'60' - X'6F'
         DC    C'         `:#@''="'     X'70' - X'7F'
         DC    C'                '      X'80' - X'8F'
         DC    C'                '      X'90' - X'9F'
         DC    C'                '      X'A0' - X'AF'
         DC    C'                '      X'B0' - X'BF'
         DC    C' ABCDEFGHI      '      X'C0' - X'CF'
         DC    C' JKLMNOPQR      '      X'D0' - X'DF'
         DC    C'\ STUVWXYZ      '      X'E0' - X'EF'
         DC    C'0123456789      '      X'F0' - X'FF'
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*         PROCESS HEX FIELDS                                          *
*                                                                     *
*---------------------------------------------------------------------*
PROC2000 DS    0H
         SH    R4,H9                    LENGTH OF NAME AND ':'
         BNH   PRT0000                  NAME WON'T FIT ON THIS LINE
         LH    R1,DSPLDLEN              SOURCE LENGTH
         SLL   R1,1                     DOUBLE IT
         CH    R1,H115                  EXCEED MAXIMUM??
         BNH   PROC2010                 NO
         LH    R1,H115                  LIMIT LENGTH
PROC2010 DS    0H
         CR    R4,R1                    WILL EXPANDED DATA FIT??
         BL    PRT0000                  NO ... PRINT WHAT WE HAVE
         MVC   0(8,R3),DSPLNAME         MOVE DATA ELEMENT NAME
         MVI   8(R3),C':'               INSERT COLON
         LA    R3,9(R3)                 NEW OUTPUT ADDRESS
         LTR   R1,R1                    DATA LENGTH ZERO??
         BZ    PROC0020                 YES ... NO DATA
         LTR   R1,R1                    DATA LENGTH ZERO??
         SR    R4,R1                    MINUS EXPANDED LENGTH
         SRL   R1,1                     ORIGINAL LENGTH
PROC2020 DS    0H
         PACK  0(1,R3),0(1,R5)          MOVE A BYTE
         MVC   1(1,R3),0(R5)            MOVE IT AGAIN
         NC    0(2,R3),PROC20F          NULLIFY ZONES
         TR    0(2,R3),PROC2TBL         TRANSLATE
         LA    R3,2(R3)                 NEW OUTPUT ADDRESS
         LA    R5,1(R5)                 NEXT SOURCE BYTE
         BCT   R1,PROC2020              LOOP
         B     PROC0020                 NEXT DATA ELEMENT
PROC20F  DC    X'0F0F'
PROC2TBL DC    C'0123456789ABCDEF'
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*         PROCESS PACKED DECIMAL FIELDS                               *
*                                                                     *
*---------------------------------------------------------------------*
PROC3000 DS    0H
         SH    R4,H9                    LENGTH OF NAME AND ':'
         BNH   PRT0000                  NAME WON'T FIT ON THIS LINE
         LH    R14,DSPLDLEN             SOURCE LENGTH
         CH    R14,H15                  EXCEED MAXIMUM??
         BNH   PROC3010                 NO
         AR    R5,R14                   ADD FULL LENGTH
         LH    R14,H15                  MAXIMUM LENGTH
         SR    R5,R14                   START OF TRUNCATED DATA
PROC3010 DS    0H
         SLL   R14,1                    DOUBLE IT
         CR    R4,R14                   WILL EXPANDED DATA FIT??
         BL    PRT0000                  NO ... PRINT WHAT WE HAVE
         MVC   0(8,R3),DSPLNAME         MOVE DATA ELEMENT NAME
         MVI   8(R3),C':'               INSERT COLON
         LA    R3,9(R3)                 NEW OUTPUT ADDRESS
         LTR   R14,R14                  LENGTH ZERO??
         BZ    PROC0020                 YES ... NO DATA
         SR    R4,R14                   MINUS EXPANDED LENGTH
         SRL   R14,1                    SOURCE LENGTH
         BCTR  R14,0                    ADJUST FOR EXECUTE
         EX    R14,PROC3ZAP             MOVE TO DOUBLE WORD
         MVC   PROC3EWK,PROC3EWD        MOVE EDIT WORD
         EDMK  PROC3EWK,PROC3DWD        EDIT
         LTR   R1,R1                    R1 SET??
         BZ    PROC3020                 NO
         BCTR  R1,0                     ADJUST ADDRESS BACK 1
         MVC   0(1,R1),PROC3EWK+16      MOVE SIGN FROM END
PROC3020 DS    0H
         LA    R2,PROC3EWK+15           LAST BYTE OF EDITED RESULT
         LA    R14,1(R14)               RESTORE FULL LENGTH
         SLL   R14,1                    LENGTH OF EXPANDED RESULT
         BCTR  R14,0                    MINUS 1
         SR    R2,R14                   OUTPUT PORTION OF RESULT
         EX    R14,PROC3MVC             MOVE RESULT TO PRINT LINE
         LA    R3,1(R14,R3)             ADJUST OUTPUT ADDRESS
         B     PROC0020                 NEXT DATA ELEMENT
PROC3ZAP ZAP   PROC3DWD(8),0(0,R5)      MOVE TO DOUBLE WORD
PROC3MVC MVC   0(0,R3),0(R2)            MOVE RESULT TO PRINT
PROC3DWD DC    D'0'
*                                     1 1 1 1 1 1
*                   1 2 3 4 5 6 7 8 9 0 1 2 3 4 5
PROC3EWD DC    X'4020202020202020202020202020212060'
PROC3EWK DC    CL17' '
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*         PROCESS FULLWORD FIELDS                                     *
*                                                                     *
*---------------------------------------------------------------------*
PROC4000 DS    0H
         SH    R4,H9                    LENGTH OF NAME AND ':'
         BNH   PRT0000                  NAME WON'T FIT ON THIS LINE
         SH    R4,H11                   MINUS RESULTANT LENGTH
         BL    PRT0000                  NO... PRINT WHAT WE HAVE
         MVC   0(8,R3),DSPLNAME         MOVE FIELD NAME
         MVI   8(R3),C':'               INSERT COLON
         LA    R3,9(R3)                 NEW OUTPUT ADDRESS
         L     R1,0(R5)                 LOAD VALUE
         CVD   R1,PROC4DWD              CONVERT TO DECIMAL
         MVC   PROC4EWK,PROC4EWD        MOVE EDIT WORD
         EDMK  PROC4EWK,PROC4DWD+2      EDIT VALUE
         LTR   R1,R1                    R1 SET??
         BZ    PROC4010                 NO
         BCTR  R1,0                     ADJUST ADDRESS
         MVC   0(1,R1),PROC4EWK+12      MOVE SIGN
         MVC   0(11,R3),PROC4EWK+1      MOVE RESULT
PROC4010 DS    0H
         LA    R3,11(R3)                ADJUST OUTPUT ADDRESS
         B     PROC0020                 NEXT DATA ELEMENT
PROC4DWD DC    D'0'
PROC4EWK DC    CL13' '
*                                       1
*                     1 2 3 4 5 6 7 8 9 0
PROC4EWD DC    X'40202020202020202020202060'
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*         PROCESS HALFWORD FIELDS                                     *
*                                                                     *
*---------------------------------------------------------------------*
PROC5000 DS    0H
         SH    R4,H9                    LENGTH OF NAME AND ':'
         BNH   PRT0000                  NAME WON'T FIT ON THIS LINE
         SH    R4,H6                    MINUS RESULTANT LENGTH
         BL    PRT0000                  NO... PRINT WHAT WE HAVE
         MVC   0(8,R3),DSPLNAME         MOVE FIELD NAME
         MVI   8(R3),C':'               INSERT COLON
         LA    R3,9(R3)                 NEW OUTPUT ADDRESS
         LH    R1,0(R5)                 LOAD VALUE
         CVD   R1,PROC5DWD              CONVERT TO DECIMAL
         MVC   PROC5EWK,PROC5EWD        MOVE EDIT WORD
         EDMK  PROC5EWK,PROC5DWD+5      EDIT VALUE
         LTR   R1,R1                    R1 SET??
         BZ    PROC5010                 NO
         BCTR  R1,0                     ADJUST ADDRESS
         MVC   0(1,R1),PROC5EWK+6       MOVE SIGN
         MVC   0(6,R3),PROC5EWK         MOVE RESULT
PROC5010 DS    0H
         LA    R3,6(R3)                 ADJUST OUTPUT ADDRESS
         B     PROC0020                 NEXT DATA ELEMENT
PROC5DWD DC    D'0'
PROC5EWK DC    CL7' '
*
*                   1 2 3 4 5
PROC5EWD DC    X'40202020212060'
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*         PROCESS BINARY FIELDS                                       *
*                                                                     *
*---------------------------------------------------------------------*
PROC6000 DS    0H
         SH    R4,H9                    LENGTH OF NAME AND ':'
         BNH   PRT0000                  NAME WON'T FIT ON THIS LINE
         LH    R1,DSPLDLEN              SOURCE LENGTH
         CH    R1,H12                   EXCEED MAXIMUM??
         BNH   PROC6010                 NO
         LH    R1,H12                   LIMIT DATA LENGTH
PROC6010 DS    0H
         SLL   R1,3                     FOR 8 BITS PER BYTE
         SR    R4,R1                    WILL EXPANDED DATA FIT??
         BL    PRT0000                  NO... PRINT WHAT WE HAVE
         MVC   0(8,R3),DSPLNAME         MOVE DATA ITEM NAME
         MVI   8(R3),C':'               INSERT COLON
         LA    R3,9(R3)                 UPDATE OUTPUT ADDRESS
         LTR   R1,R1                    DATA LENGTH ZERO??
         BZ    PROC0020                 YES ... NO DATA
         SRL   R1,3                     RESET TO SOURCE LENGTH
         MVC   0(8,R3),PROC6INT         INITIALZE TO '00000000'
         TM    0(R5),X'80'              BIT ON??
         BNO   PROC6020                 NO
         MVI   0(R3),C'1'               CHANGE TO '1'
PROC6020 DS    0H
         TM    0(R5),X'40'              BIT ON??
         BNO   PROC6030                 NO
         MVI   1(R3),C'1'               CHANGE TO '1'
PROC6030 DS    0H
         TM    0(R5),X'20'              BIT ON??
         BNO   PROC6040                 NO
         MVI   2(R3),C'1'               CHANGE TO '1'
PROC6040 DS    0H
         TM    0(R5),X'10'              BIT ON??
         BNO   PROC6050                 NO
         MVI   3(R3),C'1'               CHANGE TO '1'
PROC6050 DS    0H
         TM    0(R5),X'08'              BIT ON??
         BNO   PROC6060                 NO
         MVI   4(R3),C'1'               CHANGE TO '1'
PROC6060 DS    0H
         TM    0(R5),X'04'              BIT ON??
         BNO   PROC6070                 NO
         MVI   5(R3),C'1'               CHANGE TO '1'
PROC6070 DS    0H
         TM    0(R5),X'02'              BIT ON??
         BNO   PROC6080                 NO
         MVI   6(R3),C'1'               CHANGE TO '1'
PROC6080 DS    0H
         TM    0(R5),X'01'              BIT ON??
         BNO   PROC6090                 NO
         MVI   7(R3),C'1'               CHANGE TO '1'
PROC6090 DS    0H
         LA    R3,8(R3)                 ADJUST OUTPUT ADDRESS
         LA    R5,1(R5)                 ADJUST SOURCE ADDRESS
         BCT   R1,PROC6010              LOOP
         B     PROC0020                 NEXT DATA ELEMENT
PROC6INT DC    C'00000000'
*---------------------------------------------------------------------*
*                                                                     *
*         PROCESS REGISTER CONTENT FIELDS                             *
*                                                                     *
*---------------------------------------------------------------------*
PROC7000 DS    0H
         SH    R4,H9                    LENGTH OF NAME AND ':'
         BNH   PRT0000                  NAME WON'T FIT ON THIS LINE
         LH    R1,DSPLDLEN              SOURCE LENGTH
         SLL   R1,1                     DOUBLE IT
         CH    R1,H115                  EXCEED MAXIMUM??
         BNH   PROC7010                 NO
         LH    R1,H115                  LIMIT LENGTH
PROC7010 DS    0H
         CR    R4,R1                    WILL EXPANDED DATA FIT??
         BL    PRT0000                  NO ... PRINT WHAT WE HAVE
         MVC   0(8,R3),DSPLNAME         MOVE DATA ELEMENT NAME
         MVI   8(R3),C':'               INSERT COLON
         LA    R3,9(R3)                 NEW OUTPUT ADDRESS
         UNPK  0(9,R3),DSPLADDR(5)      MOVE A BYTE
         MVZ   0(8,R3),PROC70F          NULLIFY ZONES
         TR    0(8,R3),PROC7TBL         TRANSLATE
         MVI   8(R3),C' '               RESTORE BLANK
         LA    R3,8(R3)                 NEXT OUTPUT ADDRESS
         SR    R4,R1                    MINUS LENGTH USED
         B     PROC0020                 NEXT DATA ELEMENT
PROC70F  DC    9X'00'
PROC7TBL DC    C'0123456789ABCDEF'
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*         PRINT                                                       *
*                                                                     *
*---------------------------------------------------------------------*
PRT0000  DS    0H
         PUT   DISPDCB,PLINE            PRINT DATA BUILT
         AP    LINECT,P1                ADD 1 TO LINE COUNT
         CLI   PLINE,C'0'               DOUBLE SPACE??
         BNE   PRT0010                  NO
         AP    LINECT,P1                ADD 1 TO LINE COUNT
PRT0010  DS    0H
         CP    LINECT,P65               PAGE FULL??
         MVC   PLINE,PLINE-1            CLEAR PRINT LINE
         BL    PRT0020                  NO
         BAL   R10,HEADINGS             PRINT HEADING
PRT0020  DS    0H
         LA    R3,PRDATA                INITIALIZE DATA ADDRESS
         LA    R4,124                   INITIALIZE LENGTH REMAINING
         B     PROC0030                 RE-TRY LAST ELEMENT
HEADINGS DS    0H
         AP    PAGECT,P1                ADD 1 TO PAGE COUNT
         MVC   PRPAGE,PAGEEDWD          MOVE PAGE EDIT WORD
         ED    PRPAGE,PAGECT            EDIT PAGE COUNT
         PUT   DISPDCB,HEADLINE         PRINT HEADING
         ZAP   LINECT,P0                RESET LINE COUNT
         BR    R10                      RETURN
EXIT0000 DS    0H
         PUT   DISPDCB,PLINE            PRINT DATA BUILT
         AP    LINECT,P1                ADD 1 TO LINE COUNT
         MVC   PLINE,PLINE-1            CLEAR PRINT LINE
         CP    LINECT,P65               PAGE FULL??
         BNH   EXIT0010                 NO
         BAL   R10,HEADINGS             PRINT HEADING
EXIT0010 DS    0H
         L     R13,SAVE+4               RE-LOAD SAVE AREA POINTER
         LM    R14,R12,12(R13)          RESTORE REGISTERS
         SR    R15,R15                  RETURN CODE
         BR    R14                      RETURN TO CALLER
*---------------------------------------------------------------------*
*                      DATA AREAS                                     *
*---------------------------------------------------------------------*
         LTORG
SAVE     DC    9D'0'                 REGISTER SAVE AREA
H2       DC    H'2'
H6       DC    H'6'
H9       DC    H'9'
H11      DC    H'11'
H12      DC    H'12'
H15      DC    H'15'
H115     DC    H'115'
P0       DC    P'0'
P1       DC    P'1'
P65      DC    P'65'
LINECT   DC    PL3'0'
PAGECT   DC    PL3'0'
PAGEEDWD DC    X'402020202120'
SEQNBR   DC    PL4'0'
SEQWORK  DC    CL8' '
SEQEDWD  DC    X'4020202020202120'
DCBFLAG  DC    X'00'
$DCBOPEN EQU   X'80'
HEADLINE DC    CL123'1SEQ NBR'
         DC    C'PAGE'
PRPAGE   DC    CL6' '
         DC    C' '
PLINE    DS    0CL132
PRCC     DC    C'0'
PRSEQ    DC    CL7' '
         DC    C' '
PRDATA   DC    CL124' '
         SPACE
*---------------------------------------------------------------------*
*                      DCB'S                                          *
*---------------------------------------------------------------------*
DISPDCB  DCB   DSORG=PS,                                               +
               RECFM=FBA,                                              +
               LRECL=133,                                              +
               DDNAME=DISPLAY,                                         +
               MACRF=PM
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
DSPLDSCT DSECT
DSPLNAME DS    CL8
DSPLADDR DS    AL4
DSPLTYPE DS    C
DSPLDLEN DS    AL2
DSPLLEN  EQU   *-DSPLDSCT
*---------------------------------------------------------------------*
*                      EQUATES                                        *
*---------------------------------------------------------------------*
R0       EQU   0                   . R
R1       EQU   1                   .  E
R2       EQU   2                   .   G
R3       EQU   3                   .    I
R4       EQU   4                   .     S
R5       EQU   5                   .      T
R6       EQU   6                   .       E
R7       EQU   7                   .        R
R8       EQU   8                   .
R9       EQU   9                   .          E
R10      EQU   10                  .           Q
R11      EQU   11                  .            U
R12      EQU   12                  .             A
R13      EQU   13                  .              T
R14      EQU   14                  .               E
R15      EQU   15                  .                S
         SPACE 2
         END   DISPLAY
